plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            keyAlias rootProject.ext.signRelease.keyAlias
            keyPassword rootProject.ext.signRelease.keyPassword
            storeFile rootProject.ext.signRelease.storeFile
            storePassword rootProject.ext.signRelease.storePassword
        }
    }
    flavorDimensions "channel", "env"
    productFlavors {
        official {
            dimension 'channel'
        }
        unofficial {
            dimension 'channel'
        }
        internal {
            applicationId = rootProject.ext.android.applicationIdInternal
            dimension 'env'
        }
        production {
            applicationId = rootProject.ext.android.applicationId
            dimension 'env'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (rootProject.ext.signRelease.storeFile.exists()) {
                println("release: using jks ${rootProject.ext.signRelease.storeFile}")
                signingConfig signingConfigs.release
            } else {
                println("no jks, use defalut signing")
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-Debug"
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        beta {
            initWith release
            applicationIdSuffix ".beta"
            versionNameSuffix "-Beta"
            debuggable true
        }
    }
}

dependencies {
    testImplementation rootProject.ext.testDependencies["junit"]
    androidTestImplementation rootProject.ext.testDependencies["runner"]
    androidTestImplementation rootProject.ext.testDependencies["espressoCore"]

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    implementation rootProject.ext.dependencies["appcompat"]
    implementation rootProject.ext.dependencies["constraintLayout"]

    implementation project(":demo-lib-android")
//    implementation 'com.github.CodePoem.UploadRepository:demo-lib-android:1.0.9'
//    implementation 'com.github.CodePoem.UploadRepository:demo-lib-java:1.0.9'
//    implementation 'com.github.CodePoem.UploadRepository:demo-lib-kotlin:1.0.9'
//    implementation 'com.github.CodePoem.UploadRepository:demo-base-android:1.0.9'

}